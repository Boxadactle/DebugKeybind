plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
    id "maven-publish"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modApi("dev.boxadactle.boxlib:BoxLib-forge:${rootProject.boxlib_version}")
    include("dev.boxadactle.boxlib:BoxLib-forge:${rootProject.boxlib_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")

    manifest {
        attributes([
                "MixinConfigs": "DebugKeybind-common.mixins.json"
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

def modrinthToken = System.getenv().MODRINTH_TOKEN
def curseforgeToken = System.getenv().CURSEFORGE_TOKEN

unifiedPublishing {
    project {
        displayName = "[Forge ${rootProject.supported_mc_version}] v${rootProject.mod_version}"
        releaseType = "release"
        gameVersions.addAll("1.20.1", "1.20")
        gameLoaders.add("forge")

        mainPublication remapJar

        if (curseforgeToken != null) {
            curseforge {
                token = curseforgeToken
                id = project.curseforge_project_id
                gameVersions.addAll("Java 17")
            }
        }

        if (modrinthToken != null) {
            modrinth {
                token = modrinthToken
                id = project.modrinth_project_id
                version = "${rootProject.mod_version}-forge"
            }
        }
    }
}

rootProject.tasks.getByName("modPublish").dependsOn("publishUnified")